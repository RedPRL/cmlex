name ParseMainFun

terminal IDENT of pos_string
terminal NUMBER of pos_int
terminal STRING of pos_intlist
terminal ALPHABET of pos
terminal AND of pos
terminal ANY of pos
terminal ARROW of pos
terminal COLON of pos
terminal EOS of pos
terminal EMPT of pos
terminal ENABLE of pos
terminal EPSILON of pos
terminal EQUAL of pos
terminal FUNCTION of pos
terminal GEQ of pos
terminal LPAREN of pos
terminal MINUS of pos
terminal NAME of pos
terminal OR of pos
terminal PLUS of pos
terminal QUESTION of pos
terminal RANGE of pos
terminal REGEXP of pos
terminal REPEAT of pos
terminal RPAREN of pos
terminal SEQ of pos
terminal SET of pos
terminal STAR of pos
terminal TILDE of pos


nonterminal Ident : string =
   1:IDENT => ident

nonterminal Number : int =
   1:NUMBER => number

nonterminal NumPairs : numpairs =
   => nil_numpairs
   1:Number 2:Number 3:NumPairs => cons_numpairs

nonterminal Charset : charset =
   1:Ident => ident_charset
   1:Number => number_charset
   LPAREN RANGE 1:NumPairs RPAREN => range_charset
   EMPT => empty_charset
   LPAREN OR 1:Charsets RPAREN => union_charset
   LPAREN AND 1:Charsets RPAREN => intersect_charset
   LPAREN MINUS 1:Charset 2:Charsets RPAREN => diff_charset
   LPAREN TILDE 1:Charsets RPAREN => comp_charset
   ANY => any_charset

nonterminal Charsets : charsets =
   => nil_charsets
   1:Charset 2:Charsets => cons_charsets
   
nonterminal Regexp : regexp =
   1:Ident => ident_regexp
   1:Number => number_regexp
   1:STRING => string_regexp
   ANY => any_regexp
   EPSILON => epsilon_regexp
   EMPT => empty_regexp
   LPAREN COLON 1:Regexps RPAREN => concat_regexp
   LPAREN SEQ 1:Regexps RPAREN => seq_regexp
   LPAREN OR 1:Regexps RPAREN => union_regexp
   LPAREN QUESTION 1:Regexp RPAREN => option_regexp
   LPAREN STAR 1:Regexp RPAREN => closure_regexp
   LPAREN PLUS 1:Regexp RPAREN => plus_regexp
   LPAREN EQUAL 2:Number 1:Regexp RPAREN => equal_regexp
   LPAREN GEQ 2:Number 1:Regexp RPAREN => geq_regexp
   LPAREN REPEAT 2:Number 3:Number 1:Regexp RPAREN => repeat_regexp
   EOS => eos_regexp

nonterminal Regexps : regexps =
   => nil_regexps
   1:Regexp 2:Regexps => cons_regexps
   
nonterminal Arm : arm =
   1:Regexp ARROW 2:Ident => sole_arm

nonterminal Arms : arms =
   1:Arm => sing_arms
   1:Arm 2:Arms => cons_arms

nonterminal Directive : directive =
   NAME 1:Ident => name_directive
   ENABLE 1:Ident => enable_directive
   ALPHABET 1:Number => alphabet_directive
   REGEXP 1:Ident EQUAL 2:Regexp => regexp_directive
   SET 1:Ident EQUAL 2:Charset => set_directive
   FUNCTION 1:Ident COLON 2:Ident EQUAL 3:Arms => function_directive

nonterminal Directives : directives =
   => nil_directives
   1:Directive 2:Directives => cons_directives

start Directives
