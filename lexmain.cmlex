name LexMainFun
alphabet 128

set alpha = (range 'a 'z 'A 'Z)
set digit = (range '0 '9)
set alphanumeric = (| alpha digit '_ '')
set white = (| 32 9 10 13)  /* space, tab, lf, cr */
set printable = (range '! '~)
set not_dquote = (~ '")

regexp ident = (seq alpha (* alphanumeric))
regexp ws = (+ white)

type t

action eof : t
action ident : t
action number : t
action skip : t
action char : t
action lcomment : t
action string : t
action error : t

action ampersand : t
action arrow : t
action bar : t
action colon : t
action equal : t
action lparen : t
action minus : t
action plus : t
action question : t
action rparen : t
action slash : t
action star : t
action tilde : t

function lexmain =
   eos => eof
   ident => ident
   (+ digit) => number
   ws => skip
   (seq '' printable) => char
   "/*" => lcomment
   (seq '" (* not_dquote) '") => string
   '& => ampersand
   "=>" => arrow
   '| => bar
   ': => colon
   '= => equal
   '( => lparen
   '- => minus
   '+ => plus
   '? => question
   ') => rparen
   '/ => slash
   '* => star
   '~ => tilde
   epsilon => error

type u

action comment_open : u
action comment_close : u
action comment_skip : u
action comment_error : u

function skipcomment =
   "/*" => comment_open
   "*/" => comment_close
   any => comment_skip
   epsilon => comment_error
